buildscript {
    repositories {
        google()
        jcenter()
        maven { url "http://kotlin.bintray.com/kotlinx" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_android_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.moowork.gradle:gradle-node-plugin:$gradle_node_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_native_version"
    }
}

plugins {
    id 'cz.malohlava' version '1.0.3'
}

allprojects {
    group 'org.jetbrains'
    version '1.0-SNAPSHOT'

    repositories {
        google()
        jcenter()
        maven { url "http://kotlin.bintray.com/kotlinx" }
    }

    tasks.withType(Test) {
        testLogging {
            showStandardStreams = true
            events "passed", "failed"
        }
    }
}

task convertGraph(type: Exec) {
    description 'Convert the task dependency graph to svg'
    workingDir project.buildDir
    commandLine 'dot', '-Tsvg', 'reports/visteg.dot', '-o', 'reports/visteg.svg'
    doLast {
        println "Task dependency graph is available in file://$project.buildDir/reports/visteg.svg"
    }
}

task graph(type: Exec) {
    dependsOn convertGraph
    group 'Custom'
    description 'Opens the task dependency graph in Safari'
    workingDir project.buildDir
    def isMacOs = System.properties['os.name'].toLowerCase().startsWith('mac')
    if (isMacOs) {
        commandLine 'open', '-a', 'Safari', 'reports/visteg.svg'
    }
}
